Import("environment")

import os

if os.name == "posix":
	monitor_src = "posix_monitor.c"
else:
	monitor_src = "win32_monitor.c"

source = [
	"group.c",
	"image.c",
	"shape.c",
	"context.c",
	"thread.c",
	monitor_src
]

backends = ["Software"]

conf = Configure(environment)
if conf.CheckCHeader("vulkan/vulkan.h"):
    backends += ["Vulkan"]
if conf.CheckCHeader("GL/gl.h"):
    backends += ["OpenGL"]

enable_egl = conf.CheckCHeader("EGL/egl.h")

conf.Finish()

if enable_egl:
    environment.Append(CPPDEFINES="SAPPHIRESG_ENABLE_EGL")
else:
    environment.Append(CPPDEFINES="SAPPHIRESG_DISABLE_EGL")

libs = SConscript(dirs = backends, exports = ["environment"])

libsapphiresg = environment.SharedLibrary("sapphiresg", source, LIBS = libs)

Return("libsapphiresg")
